package alarms

import rs.sbnz.book_recommender.model.User;

declare SystemGradeEvent
    @role(event)
    @expires(25s)
    userId: Integer
end

declare SuspiciousUserAlarm
    @role(event)
    @expires(25s)
    userId: Integer
    reason: String
end

declare BlockUserScoringEvent
    @role(event)
    @expires(4h)
    userId: Integer
end

rule "Insert user grading event"
    agenda-group "UserAlarms"
	salience 10
    when
        $u: User(id: id)
    then
        SystemGradeEvent gradeEvent = new SystemGradeEvent();
        System.out.println("Dodajem");
        gradeEvent.setUserId(id);
        insert(gradeEvent);
        //delete($u);
end

rule "Potentially suspicious user alarm"
    agenda-group "UserAlarms"
    salience 7
    when
        User(id: id)
        $sde:SystemGradeEvent(userId == id)
        $num: Number(intValue > 9) from accumulate(
            $sde2: SystemGradeEvent(
                this != $sde,
                userId == id,
                this meets[100s] $sde
            ),
            count($sde2)
        )
        not (SuspiciousUserAlarm(userId == id, this meets[25s] $sde))
    then
        System.out.println("Ulazim ovde alarm");
        System.out.println($num);
        SuspiciousUserAlarm alarm = new SuspiciousUserAlarm();
        alarm.setUserId(id);
        alarm.setReason("Too many book reviews in small time span");
        insert(alarm);
end


rule "Suspicious user alarm"
    agenda-group "UserAlarms"
    salience 5
    when
        User(id: id)
        $sde:SystemGradeEvent(userId == id)
        $num: Number(intValue > 19) from accumulate(
            $sde2: SystemGradeEvent(
                this != $sde,
                userId == id,
                this meets[100s] $sde
            ),
            count($sde2)
        )
        SuspiciousUserAlarm(userId == id, reason == "Too many book reviews in small time span")
        not (BlockUserScoringEvent(userId == id))
    then
        System.out.println("User je los");
        System.out.println($num);
        BlockUserScoringEvent blockEvent = new BlockUserScoringEvent();
        blockEvent.setUserId(id);
        insert(blockEvent);
end

rule "Block User scoring ability"
    agenda-group "UserAlarms"
    salience 4
    when
        $u: User(id: id, blockedScoringFunction == 0)
        $bse: BlockUserScoringEvent(userId == id)
    then
        System.out.println("User blocked");
		modify($u){setBlockedScoringFunction(1)}

end

rule "Remove User from memory"
    agenda-group "UserAlarms"
    salience 2
    when
        $u: User()
    then
        System.out.println("Brise");
        delete($u);
        System.out.println("Obrisao");
        drools.halt();

end

